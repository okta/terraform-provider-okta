---
page_title: "Resource: okta_app_oauth"
description: |-
  This resource allows you to create and configure an OIDC Application.
  -> During an apply if there is change in status the app will first be
  activated or deactivated in accordance with the status change. Then, all
  other arguments that changed will be applied.
---

# Resource: okta_app_oauth

This resource allows you to create and configure an OIDC Application.
-> During an apply if there is change in status the app will first be
activated or deactivated in accordance with the status change. Then, all
other arguments that changed will be applied.

~> `okta_app_oauth_redirect_uri` has been marked deprecated and will be removed
in the v5 release of the provider. Operators should manage the redirect URIs for
an oauth app directly on that resource.

## Example Usage

```terraform
resource "okta_app_oauth" "example" {
  label          = "example"
  type           = "web"
  grant_types    = ["authorization_code"]
  redirect_uris  = ["https://example.com/"]
  response_types = ["code"]
}

### With JWKS value
### See also [Advanced PEM secrets and JWKS example](#advanced-pem-and-jwks-example).

resource "okta_app_oauth" "example" {
  label                      = "example"
  type                       = "service"
  response_types             = ["token"]
  grant_types                = ["client_credentials"]
  token_endpoint_auth_method = "private_key_jwt"

  jwks {
    kty = "RSA"
    kid = "SIGNING_KEY_RSA"
    e   = "AQAB"
    n   = "xyz"
  }

  jwks {
    kty = "EC"
    kid = "SIGNING_KEY_EC"
    x   = "K37X78mXJHHldZYMzrwipjKR-YZUS2SMye0KindHp6I"
    y   = "8IfvsvXWzbFWOZoVOMwgF5p46mUj3kbOVf9Fk0vVVHo"
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `label` (String) The Application's display name.
- `type` (String) The type of client application.

### Optional

- `accessibility_error_redirect_url` (String) Custom error page URL
- `accessibility_login_redirect_url` (String) Custom login page URL
- `accessibility_self_service` (Boolean) Enable self service. Default is `false`
- `admin_note` (String) Application notes for admins.
- `app_links_json` (String) Displays specific appLinks for the app. The value for each application link should be boolean.
- `app_settings_json` (String) Application settings in JSON format
- `authentication_policy` (String) The ID of the associated app_signon_policy. If this property is removed from the application the default sign-on-policy will be associated with this application. From now on, there is no need to attach authentication_policy for applications of type `SERVICE` in the upcoming release.
- `auto_key_rotation` (Boolean) Requested key rotation mode. If
				auto_key_rotation isn't specified, the client automatically opts in for Okta's
				key rotation. You can update this property via the API or via the administrator
				UI.
				See: https://developer.okta.com/docs/reference/api/apps/#oauth-credential-object"
- `auto_submit_toolbar` (Boolean) Display auto submit toolbar
- `client_basic_secret` (String, Sensitive) The user provided OAuth client secret key value, this can be set when token_endpoint_auth_method is client_secret_basic. This does nothing when `omit_secret is set to true.
- `client_id` (String) OAuth client ID. If set during creation, app is created with this id.
- `client_uri` (String) URI to a web page providing information about the client.
- `consent_method` (String) *Early Access Property*. Indicates whether user consent is required or implicit. Valid values: REQUIRED, TRUSTED. Default value is TRUSTED. Note: Enable `API_ACCESS_MANAGEMENT`, `API_ACCESS_MANAGEMENT_CONSENT` feature flags in your org to use this property.
- `enduser_note` (String) Application notes for end users.
- `grant_types` (Set of String) List of OAuth 2.0 grant types. Conditional validation params found here https://developer.okta.com/docs/api/resources/apps#credentials-settings-details. Defaults to minimum requirements per app type.
- `groups_claim` (Block Set, Max: 1) Groups claim for an OpenID Connect client application (argument is ignored when API auth is done with OAuth 2.0 credentials) (see [below for nested schema](#nestedblock--groups_claim))
- `hide_ios` (Boolean) Do not display application icon on mobile app
- `hide_web` (Boolean) Do not display application icon to users
- `implicit_assignment` (Boolean) *Early Access Property*. Enable Federation Broker Mode.
- `issuer_mode` (String) *Early Access Property*. Indicates whether the Okta Authorization Server uses the original Okta org domain URL or a custom domain URL as the issuer of ID token for this client.
- `jwks` (Block List) (see [below for nested schema](#nestedblock--jwks))
- `jwks_uri` (String) URL reference to JWKS
- `login_mode` (String) The type of Idp-Initiated login that the client supports, if any
- `login_scopes` (Set of String) List of scopes to use for the request
- `login_uri` (String) URI that initiates login.
- `logo` (String) Local file path to the logo. The file must be in PNG, JPG, or GIF format, and less than 1 MB in size.
- `logo_uri` (String) URI that references a logo for the client.
- `omit_secret` (Boolean) This tells the provider not manage the client_secret value in state. When this is false (the default), it will cause the auto-generated client_secret to be persisted in the client_secret attribute in state. This also means that every time an update to this app is run, this value is also set on the API. If this changes from false => true, the `client_secret` is dropped from state and the secret at the time of the apply is what remains. If this is ever changes from true => false your app will be recreated, due to the need to regenerate a secret we can store in state.
- `pkce_required` (Boolean) Require Proof Key for Code Exchange (PKCE) for additional verification key rotation mode. See: https://developer.okta.com/docs/reference/api/apps/#oauth-credential-object
- `policy_uri` (String) URI to web page providing client policy document.
- `post_logout_redirect_uris` (Set of String) List of URIs for redirection after logout. Note: see okta_app_oauth_post_logout_redirect_uri for appending to this list in a decentralized way.
- `profile` (String) Custom JSON that represents an OAuth application's profile
- `redirect_uris` (List of String) List of URIs for use in the redirect-based flow. This is required for all application types except service. Note: see okta_app_oauth_redirect_uri for appending to this list in a decentralized way.
- `refresh_token_leeway` (Number) *Early Access Property* Grace period for token rotation, required with grant types refresh_token
- `refresh_token_rotation` (String) *Early Access Property* Refresh token rotation behavior, required with grant types refresh_token
- `response_types` (Set of String) List of OAuth 2.0 response type strings. Valid values are any combination of: `code`, `token`, and `id_token`.
- `status` (String) Status of application. By default, it is `ACTIVE`
- `timeouts` (Block, Optional) (see [below for nested schema](#nestedblock--timeouts))
- `token_endpoint_auth_method` (String) Requested authentication method for the token endpoint, valid values include:  'client_secret_basic', 'client_secret_post', 'client_secret_jwt', 'private_key_jwt', 'none', etc.
- `tos_uri` (String) URI to web page providing client tos (terms of service).
- `user_name_template` (String) Username template. Default: `${source.login}`
- `user_name_template_push_status` (String) Push username on update. Valid values: `PUSH` and `DONT_PUSH`
- `user_name_template_suffix` (String) Username template suffix
- `user_name_template_type` (String) Username template type. Default: `BUILT_IN`
- `wildcard_redirect` (String) *Early Access Property*. Indicates if the client is allowed to use wildcard matching of redirect_uris.
- `participate_slo` (Boolean) *Early Access Property*. Allows the app to participate in front-channel Single Logout. Note: You can only enable participate_slo for web and browser application types. When set to true, frontchannel_logout_uri must also be provided. Enable `SINGLE_LOGOUT_SUPPORT` feature flag in your org to use this property.
- `frontchannel_logout_uri` (String) *Early Access Property*. URL where Okta sends the logout request. Required when participate_slo is true.
- `frontchannel_logout_session_required` (Boolean) *Early Access Property*. Determines whether Okta sends sid and iss in the logout request.


### Read-Only

- `client_secret` (String, Sensitive) OAuth client secret value, this is output only. This will be in plain text in your statefile unless you set omit_secret above.
- `id` (String) The ID of this resource.
- `logo_url` (String) URL of the application's logo
- `name` (String) Name of the app.
- `sign_on_mode` (String) Sign on mode of application.

<a id="nestedblock--groups_claim"></a>
### Nested Schema for `groups_claim`

Required:

- `name` (String) Name of the claim that will be used in the token.
- `type` (String) Groups claim type.
- `value` (String) Value of the claim. Can be an Okta Expression Language statement that evaluates at the time the token is minted.

Optional:

- `filter_type` (String) Groups claim filter. Can only be set if type is FILTER.

Read-Only:

- `issuer_mode` (String) Issuer mode inherited from OAuth App


<a id="nestedblock--jwks"></a>
### Nested Schema for `jwks`

Required:

- `kid` (String) Key ID
- `kty` (String) Key type

Optional:

- `e` (String) RSA Exponent
- `n` (String) RSA Modulus
- `x` (String) X coordinate of the elliptic curve point
- `y` (String) Y coordinate of the elliptic curve point


<a id="nestedblock--timeouts"></a>
### Nested Schema for `timeouts`

Optional:

- `create` (String)
- `read` (String)
- `update` (String)

## Import

Import is supported using the following syntax:

```shell
terraform import okta_app_oauth.example <app_id>
```


## Etc.

### Resetting client secret

If the client secret needs to be reset run an apply with `omit_secret` set to
true in the resource. This causes `client_secret` to be set to blank. Remove
`omit_secret` and run apply again. The resource will set a new `client_secret`
for the app.

### Private Keys

The private key format that an Okta OAuth app expects is PKCS#8 (unencrypted).
The operator either uploads their own private key or Okta can generate one in
the Admin UI Panel under the apps Client Credentials. PKCS#8 format can be
identified by a header that starts with `-----BEGIN PRIVATE KEY-----`. If the
operator has a PKCS#1 (unencrypted) format private key (the header starts with
`-----BEGIN RSA PRIVATE KEY-----`) they can generate a PKCS#8 format
key with `openssl`:

```
 openssl rsa -in pkcs1.pem -out pkcs8-example.pem
```

### Advanced PEM and JWKS example

```hcl
# This example config illustrates how Terraform can be used to generate a
# private keys PEM file and valid JWKS to be used as part of an Okta OAuth
# app's creation.
#
terraform {
  required_providers {
    okta = {
      source = "okta/okta"
    }
    tls = {
      source = "hashicorp/tls"
    }
    jwks = {
      source = "iwarapter/jwks"
    }
  }
}

# NOTE: Example to generate a PEM easily as a tool. These secrets will be saved
# to the state file and shouldn't be persisted. Instead, save the secrets into
# a secrets manager to be reused.
# https://registry.terraform.io/providers/hashicorp/tls/latest/docs/resources/private_key
#
# NOTE: Even though tls is a Hashicorp provider you should still audit its code
# to be satisfied with its security.
# https://github.com/hashicorp/terraform-provider-tls
#
resource "tls_private_key" "rsa" {
  algorithm = "RSA"
  rsa_bits  = 4096
}
#
# Pretty print a PEM with TF show and jq
# terraform show -json | jq -r '.values.root_module.resources[] | select(.address == "tls_private_key.rsa").values.private_key_pem'
#
# Delete the secrets explicitly or just remove them from the config and run
# apply again.
# terraform apply -destroy -auto-approve -target=tls_private_key.rsa

# NOTE: Even though the iwarapter/jwks is listed in the registry you should
# still audit its code to be satisfied with its security.
# https://registry.terraform.io/providers/iwarapter/jwks/latest/docs/data-sources/from_key
# https://github.com/iwarapter/terraform-provider-jwks
#
data "jwks_from_key" "jwks" {
  key = tls_private_key.rsa.private_key_pem
  kid = "my-kid"
}
#
# Pretty print the jwks
# terraform show -json | jq -r '.values.root_module.resources[] | select(.address == "data.jwks_from_key.jwks").values.jwks' | jq .

# Feed values into Okta OAuth app's jwks
locals {
  jwks = jsondecode(data.jwks_from_key.jwks.jwks)
}

# https://registry.terraform.io/providers/okta/okta/latest/docs/resources/app_oauth
resource "okta_app_oauth" "app" {
  label                      = "My OAuth App"
  type                       = "service"
  response_types             = ["token"]
  grant_types                = ["client_credentials"]
  token_endpoint_auth_method = "private_key_jwt"

  jwks {
    kty = local.jwks.kty
    kid = local.jwks.kid
    e   = local.jwks.e
    n   = local.jwks.n
  }
}
#
# Pretty print OAuth app Client ID
# terraform show -json | jq -r '.values.root_module.resources[] | select(.address == "okta_app_oauth.app").values.id'
```