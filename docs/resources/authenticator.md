---
page_title: "Resource: okta_authenticator"
description: |-
  ~> WARNING: This feature is only available as a part of the Identity Engine. Contact support mailto:dev-inquiries@okta.com for further information.
  This resource allows you to configure different authenticators.
  -> Create: The Okta API has an odd notion of create for authenticators. If
  the authenticator doesn't exist then a one time 'POST /api/v1/authenticators' to
  create the authenticator (hard create) will be performed. Thereafter, that
  authenticator is never deleted, it is only deactivated (soft delete). Therefore,
  if the authenticator already exists create is just a soft import of an existing
  authenticator. This does not apply to customotp authenticator. There can be
  multiple customotp authenticator. To create new customotp authenticator, a new
  name and key = customotp is required. If an old name is used, it will simply
  reactivate the old custom_otp authenticator
  -> Update: custom_otp authenticator cannot be updated
  -> Delete: Authenticators can not be truly deleted therefore delete is soft.
  Delete will attempt to deativate the authenticator. An authenticator can only be
  deactivated if it's not in use by any other policy.
---

# Resource: okta_authenticator

~> **WARNING:** This feature is only available as a part of the Identity Engine. [Contact support](mailto:dev-inquiries@okta.com) for further information.

This resource allows you to configure different authenticators.

-> **Create:** The Okta API has an odd notion of create for authenticators. If
the authenticator doesn't exist then a one time 'POST /api/v1/authenticators' to
create the authenticator (hard create) will be performed. Thereafter, that
authenticator is never deleted, it is only deactivated (soft delete). Therefore,
if the authenticator already exists create is just a soft import of an existing
authenticator. This does not apply to custom_otp authenticator. There can be 
multiple custom_otp authenticator. To create new custom_otp authenticator, a new 
name and key = custom_otp is required. If an old name is used, it will simply 
reactivate the old custom_otp authenticator

-> **Update:** custom_otp authenticator cannot be updated

-> **Delete:** Authenticators can not be truly deleted therefore delete is soft.
Delete will attempt to deativate the authenticator. An authenticator can only be
deactivated if it's not in use by any other policy.

## Example Usage

```terraform
resource "okta_authenticator" "test" {
  name = "Security Question"
  key  = "security_question"
  settings = jsonencode(
    {
      "allowedFor" : "recovery"
    }
  )
}

resource "okta_authenticator" "otp" {
  name   = "Custom OTP"
  key    = "custom_otp"
  status = "ACTIVE"
  settings = jsonencode({
    "protocol" : "TOTP",
    "acceptableAdjacentIntervals" : 3,
    "timeIntervalInSeconds" : 30,
    "encoding" : "base32",
    "algorithm" : "HMacSHA256",
    "passCodeLength" : 6
  })
  // required to be false for custom_otp
  legacy_ignore_name = false
}

# Phone authenticator with method-level control
resource "okta_authenticator" "phone" {
  name = "Phone Authentication"
  key  = "phone_number"
  
  # Enable SMS method
  method {
    type   = "sms"
    status = "ACTIVE"
  }
  
  # Disable voice method
  method {
    type   = "voice"
    status = "INACTIVE"
  }
}

# Okta Verify with specific methods enabled
resource "okta_authenticator" "okta_verify" {
  name = "Okta Verify"
  key  = "okta_verify"
  
  method {
    type   = "push"
    status = "ACTIVE"
  }
  
  method {
    type   = "totp"
    status = "ACTIVE"
  }
  
  method {
    type   = "signed_nonce"
    status = "INACTIVE"
  }
}

# Custom OTP with method settings
resource "okta_authenticator" "custom_otp_with_method" {
  name   = "Custom OTP Authenticator"
  key    = "custom_otp"
  status = "ACTIVE"
  legacy_ignore_name = false
  
  method {
    type   = "otp"
    status = "ACTIVE"
    settings = jsonencode({
      "protocol" : "TOTP",
      "encoding" : "base32",
      "algorithm" : "HMacSHA256",
      "timeIntervalInSeconds" : 30,
      "passCodeLength" : 6,
      "acceptableAdjacentIntervals" : 3
    })
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `key` (String) A human-readable string that identifies the authenticator. Some authenticators are available by feature flag on the organization. Possible values inclue: `duo`, `external_idp`, `google_otp`, `okta_email`, `okta_password`, `okta_verify`, `onprem_mfa`, `phone_number`, `rsa_token`, `security_question`, `webauthn`
- `name` (String) Display name of the Authenticator

### Optional

- `legacy_ignore_name` (Boolean) Name does not trigger change detection (legacy behavior)
- `method` (Block Set) Method-level configuration for authenticators. Only supported for authenticators with key `phone_number`, `okta_verify`, or `custom_otp`. (see [below for nested schema](#nestedblock--method))
- `provider_auth_port` (Number) The RADIUS server port (for example 1812). This is defined when the On-Prem RADIUS server is configured. Used only for authenticators with type `security_key`.  Conflicts with `provider_json` argument.
- `provider_host` (String) (DUO specific) - The Duo Security API hostname. Conflicts with `provider_json` argument.
- `provider_hostname` (String) Server host name or IP address. Default is `localhost`. Used only for authenticators with type `security_key`. Conflicts with `provider_json` argument.
- `provider_integration_key` (String) (DUO specific) - The Duo Security integration key.  Conflicts with `provider_json` argument.
- `provider_json` (String) Provider JSON allows for expressive providervalues. This argument conflicts with the other 'provider_xxx' arguments. The [CreateProvider](https://developer.okta.com/docs/reference/api/authenticators-admin/#request) illustrates detailed provider values for a Duo authenticator. [Provider values](https://developer.okta.com/docs/reference/api/authenticators-admin/#authenticators-administration-api-object)are listed in Okta API.
- `provider_secret_key` (String) (DUO specific) - The Duo Security secret key.  Conflicts with `provider_json` argument.
- `provider_shared_secret` (String, Sensitive) An authentication key that must be defined when the RADIUS server is configured, and must be the same on both the RADIUS client and server. Used only for authenticators with type `security_key`. Conflicts with `provider_json` argument.
- `provider_user_name_template` (String) Username template expected by the provider. Used only for authenticators with type `security_key`.  Conflicts with `provider_json` argument.
- `settings` (String) Settings for the authenticator. The settings JSON contains values based on Authenticator key. It is not used for authenticators with type `security_key`
- `status` (String) Authenticator status: `ACTIVE` or `INACTIVE`. Default: `ACTIVE`

### Read-Only

- `id` (String) The ID of this resource.
- `provider_instance_id` (String) App Instance ID.
- `provider_type` (String) Provider type. Supported value for Duo: `DUO`. Supported value for Custom App: `PUSH`
- `type` (String) he type of Authenticator. Values include: `password`, `security_question`, `phone`, `email`, `app`, `federated`, and `security_key`.

<a id="nestedblock--method"></a>
### Nested Schema for `method`

Required:

- `type` (String) The method type. Supported values depend on the authenticator key:
  - For `phone_number`: `sms`, `voice`
  - For `okta_verify`: `push`, `totp`, `signed_nonce`
  - For `custom_otp`: `otp`

Optional:

- `status` (String) The method status: `ACTIVE` or `INACTIVE`. Default: `ACTIVE`
- `settings` (String) Method-specific settings as a JSON string. Settings vary by method type:
  - For `otp` method (custom_otp authenticator):
    - `protocol` (String) - Protocol type: `TOTP` or `HOTP`
    - `encoding` (String) - Encoding format: `base32` or `base64`
    - `algorithm` (String) - Hash algorithm: `HMacSHA1`, `HMacSHA256`, or `HMacSHA512`
    - `timeIntervalInSeconds` (Number) - Time step in seconds (TOTP only)
    - `passCodeLength` (Number) - Length of the OTP code
    - `acceptableAdjacentIntervals` (Number) - Number of adjacent time intervals to accept
  - For `signed_nonce` method (okta_verify authenticator):
    - `algorithms` (Array) - Supported algorithms: `ES256`, `ES384`, `ES512`, `RS256`, `RS384`, `RS512`, `EdDSA`
    - `keyProtection` (String) - Key protection level: `ANY`, `SOFTWARE`, or `HARDWARE`

## Import

Import is supported using the following syntax:

```shell
terraform import okta_authenticator.example <authenticator_id>
```
